program			: expr (SEMICOLON? expr)* SEMICOLON?

expr            : IDENTIFIER (EQ|PLUSEQ|MINUSEQ|MULEQ|DIVEQ|MODEQ|POWEQ) expr
                : comp-expr ((KEYWORD:AND|KEYWORD:OR) comp-expr)*

comp-expr       : NOT comp-expr
                : arith-expr ((EE|LT|GT|LTE|GTE) arith-expr)*

arith-expr      : term ((PLUS|MINUS) term)*

term            : factor ((MUL|DIV|MOD) factor)*

factor			: (PLUS|MINUS) factor
				: power

power		    : call (POW factor)*

call			: access (LPAREN (expr (COMMA expr)*)? RPAREN (LPAREN (expr (COMMA expr)*)? RPAREN)* (EQ expr)?)?

access     		: atom (DOT IDENTIFIER)*

atom 			: INT|FLOAT|STRING|BUILTIN
				: IDENTIFIER (INC|DEC)?
				: LPAREN expr RPAREN
				: LBRACKET program? RBRACKET
				: class-expr
				: include-expr
				: type-expr
				: list-expr
				: list-modif-expr
				: if-expr
				: for-expr
				: for-each-expr
				: while-expr
				: break-expr
				: func-def
				: return-expr
                : disp-expr
				: input-expr
				: getch-expr
				: kbhit-expr
				: rand-expr
				: int-cast
				: str-cast
				: read-expr
				: write-expr
				: readb-expr
				: writeb-expr
				: cls-expr
				: time-expr
				: cli-expr
				: os-expr

class-expr		: KEYWORD:CLASS IDENTIFIER (LPAREN IDENTIFIER RPAREN)? expr

include-expr	: KEYWORD:INCLUDE expr (KEYWORD:AS IDENTIFIER)?

type-expr		: KEYWORD:TYPE expr

list-expr       : LSQBRACKET (expr (COMMA expr)*)? RSQBRACKET

if-expr			: KEYWORD:IF expr KEYWORD:THEN? expr
				  (KEYWORD:ELIF expr KEYWORD:THEN? expr)*
				  (KEYWORD:ELSE expr)?

for-expr		: KEYWORD:FOR IDENTIFIER EQ expr KEYWORD:TO expr 
				  (KEYWORD:STEP expr)? KEYWORD:DO? expr

for-each-expr	: KEYWORD:FOR KEYWORD:EACH IDENTIFIER KEYWORD:IN expr KEYWORD:DO? expr

while-expr	    : KEYWORD:WHILE expr KEYWORD:DO? expr

break-expr      : KEYWORD:BREAK

func-def		: KEYWORD:FN KEYWORD:MUT? KEYWORD:BUILTIN? IDENTIFIER?
				  LPAREN (IDENTIFIER (COMMA IDENTIFIER)*)? RPAREN expr

return-expr		: KEYWORD:RETURN expr

disp-expr       : KEYWORD:DISP expr

input-expr		: KEYWORD:INPUT

getch-expr		: KEYWORD:GETCH

kbhit-expr		: KEYWORD:KBHIT

rand-expr		: KEYWORD:RAND

int-cast		: KEYWORD:INT expr

float-cast		: KEYWORD:FLOAT expr

str-cast		: KEYWORD:STR expr

read-expr		: KEYWORD:READ expr

write-expr		: KEYWORD:WRITE expr COMMA expr

readb-expr		: KEYWORD:READB expr

writeb-expr		: KEYWORD:WRITEB expr COMMA expr

cls-expr		: KEYWORD:CLS

time-expr		: KEYWORD:TIME

cli-expr		: KEYWORD:CLI expr

os-expr			: KEYWORD:OS
